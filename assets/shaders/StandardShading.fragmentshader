#version 330 core

// Interpolated values from the vertex shaders
in vec2 UV;
in vec3 Position_worldspace;
in vec3 Normal_cameraspace;
in vec3 EyeDirection_cameraspace;
in vec3 LightDirection_cameraspace;

// Output data
out vec4 color;

// Values that stay constant for the whole mesh.
uniform sampler2D myTextureSampler;
uniform mat4 MV;
uniform vec3 LightPosition_worldspace;

// Solid Color for green
uniform bool useSolidColor = false;
uniform vec3 solidColor = vec3(0.0, 1.0, 0.0);  // default green
uniform float solidAlpha = 1.0;  // default opaque
uniform int uEnableDirect;  // 0 = off, 1 = on
uniform float uColorIntensity = 1.0; // scales textured object brightness

void main(){

	// Light emission properties
	// You probably want to put them as uniforms
	vec3 LightColor = vec3(1,1,1);
	float LightPower = 500000.0f;  // Massive increase for large football field
	
	// Material properties
	vec3 MaterialDiffuseColor = useSolidColor
		? solidColor
		: texture( myTextureSampler, UV ).rgb;
	MaterialDiffuseColor *= uColorIntensity;

	vec3 MaterialAmbientColor = vec3(0.7,0.7,0.7) * MaterialDiffuseColor;  // High ambient for even lighting
	vec3 MaterialSpecularColor = vec3(0.3,0.3,0.3);

	// Distance to the light
	float distance = length( LightPosition_worldspace - Position_worldspace );
	
	// Greatly reduce falloff for large field
	float effectiveDistance = distance + 200.0;  // Large constant to minimize distance falloff

	// Normal of the computed fragment, in camera space
	vec3 n = normalize( Normal_cameraspace );
	// Direction of the light (from the fragment to the light)
	vec3 l = normalize( LightDirection_cameraspace );
	// Cosine of the angle between the normal and the light direction, 
	// clamped above 0
	//  - light is at the vertical of the triangle -> 1
	//  - light is perpendicular to the triangle -> 0
	//  - light is behind the triangle -> 0
	float cosTheta = clamp( dot( n,l ), 0,1 );
	
	// Eye vector (towards the camera)
	vec3 E = normalize(EyeDirection_cameraspace);
	// Direction in which the triangle reflects the light
	vec3 R = reflect(-l,n);
	// Cosine of the angle between the Eye vector and the Reflect vector,
	// clamped to 0
	//  - Looking into the reflection -> 1
	//  - Looking elsewhere -> < 1
	float cosAlpha = clamp( dot( E,R ), 0,1 );
	
	// Convert the toggle to 0.0/1.0 once
	float direct = float(uEnableDirect);

	// Ambient stays always
	vec3 ambient  = MaterialAmbientColor;

	// Direct terms
	vec3 diffuse  = MaterialDiffuseColor  * LightColor * LightPower * cosTheta / (effectiveDistance*effectiveDistance);
	vec3 specular = MaterialSpecularColor * LightColor * LightPower * pow(cosAlpha, 5.0) / (effectiveDistance*effectiveDistance);

	// Color with toggle
	vec3 finalColor = ambient + direct * (diffuse + specular);
	
	// Output with alpha channel
	float alpha = useSolidColor ? solidAlpha : 1.0;
	color = vec4(finalColor, alpha);


	//color = 
	//	// Ambient : simulates indirect lighting
	//	MaterialAmbientColor +
	//	// Diffuse : "color" of the object
	//	MaterialDiffuseColor * LightColor * LightPower * cosTheta / (distance*distance) +
	//	// Specular : reflective highlight, like a mirror
	//	MaterialSpecularColor * LightColor * LightPower * pow(cosAlpha,5) / (distance*distance);

}